# Maintainer: Dustin Falgout <dustin@falgout.us>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgbase=kate
pkgname=(kwrite kate)
pkgver=19.08.0
pkgrel=2
arch=(x86_64)
license=(GPL LGPL FDL)
makedepends=(extra-cmake-modules kdoctools plasma-framework knewstuff kitemmodels ktexteditor threadweaver kactivities)
source=(
	"https://download.kde.org/stable/applications/$pkgver/src/$pkgbase-$pkgver.tar.xz"
	0001-Defuse-root-block.patch
)
sha256sums=('6acc3172429191ab47722d788f324292ea5ee2f1e419d48c72200579b9b30878'
            'eca0530d0b697a4b454af826f93594bae48c16020e2e9aaecf2adb707963cac8')
#validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
#              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
  (cd ${srcdir}/${pkgbase}-${pkgver} && patch -Np1 -i ../0001-*.patch)
}

build() {
  cd build
  cmake ../$pkgbase-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package_kwrite() {
  pkgdesc="Text Editor"
  groups=(kde-applications kdebase)
  url="https://www.kde.org/applications/utilities/kwrite/"
  depends=(ktexteditor kactivities hicolor-icon-theme)

  cd build
  make DESTDIR="$pkgdir" install

  find "$pkgdir" -type f -name '*kate*' -exec rm {} \;
  rm -r "$pkgdir"/usr/lib/qt/plugins/ktexteditor \
        "$pkgdir"/usr/share/doc/HTML/*/{kate,katepart} \
        "$pkgdir"/usr/share/katexmltools \
        "$pkgdir"/usr/share/locale/*/LC_MESSAGES/{kterustcompletion,tabswitcherplugin,ktexteditorpreviewplugin,lspclient}.mo \
        "$pkgdir"/usr/share/plasma/plasmoids
}

package_kate() {
  pkgdesc="Advanced Text Editor"
  groups=(kde-applications kdebase)
  url="https://www.kde.org/applications/utilities/kate/"
  depends=(knewstuff ktexteditor threadweaver kactivities hicolor-icon-theme)
  optdepends=('konsole: open a terminal in Kate')

  cd build
  make DESTDIR="$pkgdir" install

  find "$pkgdir" -type f -name '*kwrite*' -exec rm {} \;
  rm -r "$pkgdir"/usr/share/doc/HTML/*/kwrite
}
